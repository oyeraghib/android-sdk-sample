apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation(name: "truesdk-0.7-releasePartner", ext: "aar")
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttp"
    implementation "com.android.support:appcompat-v7:$rootProject.androidSupportLib"
    implementation "com.android.support:design:$rootProject.androidSupportLib"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation "junit:junit:$rootProject.junit"
}

androidGitVersion {
    codeFormat 'MMNNP'
    format '%tag%'
    hideBranches = [ 'master' ]
    prefix 'v'
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.truecaller.android.sdksample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions 'partnerKey'
//    no need to assign each, since only one dimension is used
    productFlavors {
        dark {
            applicationId 'com.truecaller.sdksample2'
        }
        light {
            // uses defaultConfig
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            rootProject.renameApk(variant, output)
        }

    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}